plugins {
    id 'java'
}

group 'fr.inserm.u1078'
version Version.getVersion()

class Version{
    Version(){

    }

    static String getVersion(){
        try {
            BufferedReader br = new BufferedReader(new FileReader("src/main/resources/CHANGELOG.VCFProcessor.md"))
            String line
            while((line = br.readLine()) != null) {
                if(line.startsWith("## ")) {
                    br.close()
                    return line.split(" ")[1].split("\\(")[0]
                }
            }
            br.close()
        } catch(Exception ignored){
            //ignore
        }
        return "unknown"
    }
}

repositories {
    ivy {
        url 'https://lysine.univ-brest.fr/maven/'
        patternLayout {
            artifact 'artifact/[organisation]/[module]-[revision].[ext]'
            ivy 'ivy/[organisation]/[module]/[revision]/ivy.xml'
            m2compatible = true
        }
    }
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    implementation group: 'fr.inserm.u1078', name: 'MaokLib', version: '1.0.6'
    implementation group: 'fr.inserm.u1078', name: 'HTSJDKMaok', version: '1.0.0'
    implementation group: 'jfree', name: 'jfreechart', version: '1.0.13'
    implementation 'com.jtattoo:JTattoo:1.6.13'
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.0'
    implementation 'org.jdom:jdom2:2.0.6.1'
}

tasks.register('Release', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'fr.inserm.u1078.tludwig.vcfprocessor.Run'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

tasks.register('Deploy', Copy) {
    from "build/libs"
    into "build/libs"
    include "/${rootProject.name}-${version}.jar"
    rename { String fileName -> fileName.replace("-${version}", "") }
}

Deploy.dependsOn(Release)

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
